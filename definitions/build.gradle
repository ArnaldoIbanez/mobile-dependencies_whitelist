apply plugin: "jacoco"
apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin:'application'
apply plugin:'maven'
apply plugin:'java'

project.version = '0.1.10'
project.group = 'com.mercadolibre.melidata.catalog'

mainClassName = 'com.melidata.definitions.uploader.Uploader'

task wrapper(type: Wrapper) {
    gradleVersion = '2.1'
}

repositories {
      mavenCentral()
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:1.8.6'
    compile 'com.amazonaws:aws-java-sdk:1.9.10'
    compile 'commons-io:commons-io:2.4'
    compile project(':..:commons')
    testCompile 'junit:junit:4.+'
}

test {
  afterTest { desc, result -> 
    println "Executing test ${desc.name} [${desc.className}] with result: ${result.resultType}"
  }
}


jacoco {
  toolVersion = "0.7.1.201405082137"
  reportsDir = file("$buildDir/customJacocoReportDir")
}
jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.destination "${buildDir}/jacocoHtml"
    }
}

task coverage (dependsOn:[":test",":jacocoTestReport"]) << {
  println("Coverage, check: ${buildDir}/jacocoHtml")
}

task tests (dependsOn: 'classes', type: JavaExec) {
    main = 'tests'
    classpath = sourceSets.main.runtimeClasspath
}

task uploadCatalog(type:JavaExec) {
   main = 'com.melidata.definitions.uploader.Uploader' 
   classpath = sourceSets.main.runtimeClasspath
    systemProperty "CATALOG_DSL_FILE", System.getProperty("CATALOG_DSL_FILE")
    systemProperty "S3_BUCKET", System.getProperty("S3_BUCKET")
    systemProperty "AWS_ACCESS_KEY_ID", System.getProperty("AWS_ACCESS_KEY_ID")
    systemProperty "AWS_SECRET_KEY", System.getProperty("AWS_SECRET_KEY")
}

task validate(dependsOn: 'classes', type: JavaExec) {
    main = 'validate'
    classpath = sourceSets.main.runtimeClasspath
    systemProperty "onlyPrintFails", System.getProperty("onlyPrintFails")
    systemProperty "track", System.getProperty("track")
}

uploadArchives {
    repositories {
        mavenDeployer {
              repository(url: 'http://git.ml.com/nexus/content/repositories/releases') {
                  authentication(userName: 'mlGrailsPluginDeployer', password: '123456')
              }
              pom.groupId = project.group
              pom.artifactId = 'definitions'
              pom.version = project.version
        }
    }
}
